scilla_version 0

library ZilPayManager

(* Custom ADT storing - Title, ipfs_description, url, ipfs_image, ipfs_icon, category *)
type App =
| App of String String String (List String) String Uint32

(* Custom ADT storing - end_block_number, url, ipfs_banner_url *)
type Banner =
| Banner of Uint128 String String

type Operation =
  | Add
  | Sub

(* Error events *)
type Error =
  | CodeNotContractOwner
  | CodeOnPause
  | CodeNotValidator
  | CodeInsufficientZLPAmount
  | CodeIncorrectCategory

(* Global variables *)
let zero128 = Uint128 0
let zero256 = Uint256 0

let one_msg =
  fun (msg: Message) =>
    let nil_msg = Nil {Message} in
    Cons {Message} msg nil_msg

let option_value =
  tfun 'A =>
  fun (default: 'A) =>
  fun (opt_val: Option 'A) =>
    match opt_val with
    | Some v => v
    | None => default
    end

let option_uint128 = @option_value Uint128

let zlp_to_blocks =
    fun (amount: Uint128) =>
    fun (block_decimal: Uint128) =>
    let some_amount_128 = builtin to_uint128 amount in
    let amount_128 = option_uint128 zero128 some_amount_128 in
      builtin div amount_128 block_decimal

let get_coefficient =
  fun (value: Uint256) =>
  fun (amount: Uint256) =>
  fun (op: Operation) =>
    match op with
    | Add =>
      builtin add value amount
    | Sub =>
      let is_lt = builtin lt value amount in
        match is_lt with
        | True =>
          zero256
        | False =>
          builtin sub value amount
        end
    end

let make_error =
  fun (result : Error) =>
    let result_code =
      match result with
      | CodeNotContractOwner      => Int32 -1
      | CodeOnPause               => Int32 -2
      | CodeNotValidator          => Int32 -3
      | CodeInsufficientZLPAmount => Int32 -4
      | CodeIncorrectCategory     => Int32 -5
      end
    in
    { _exception : "Error"; code : result_code }

contract ZilPayManager

(*  Parameters *)
(
  contract_owner: ByStr20,
  zlp: ByStr20,
  validator: ByStr20
)


(* Mutable fields *)
field max_category: Uint32 = Uint32 7

field price_for_app: Uint128 = zero128
field block_decimal: Uint128 = Uint128 20000
field coefficient: Uint256 = zero256

field contract_validator: ByStr20 = validator

(* Emit Errors *)
procedure ThrowError(err : Error)
  e = make_error err;
  throw e
end

procedure IsValidator()
  contract_address <- contract_validator;
  is_validator = builtin eq contract_address _sender;

  match is_validator with
  | True =>
  | False =>
    err = CodeNotValidator;
    ThrowError err
  end
end

procedure CheckPriceForAD(amount: Uint128)
  price <- block_decimal;

  is_lt = builtin lt amount price;

  match is_lt with
  | True =>
    err = CodeInsufficientZLPAmount;
    ThrowError err
  | False =>
  end
end

procedure CheckCategory(category: Uint32)
  max <- max_category;

  is_valid = builtin lt max category;

  match is_valid with
  | True =>
    err = CodeIncorrectCategory;
    ThrowError err
  | False =>
  end
end

procedure IsContractOwner()
  is_contract_owner = builtin eq contract_owner _sender;

  match is_contract_owner with
  | True => 
  | False =>
    err = CodeNotContractOwner;
    ThrowError err
  end
end

procedure TransferFrom(amount: Uint128, from: ByStr20)
  is_zero = builtin eq zero128 amount;

  match is_zero with
  | False =>
    to <- contract_validator;

    call_transfer_zlp = {
      _tag: "TransferFrom";
      _recipient: zlp;
      _amount: Uint128 0;
      from: from;
      to: to;
      amount: amount
    };

    msgs = one_msg call_transfer_zlp;

    send msgs
  | True =>
  end
end

procedure SendAppToCheck(app: App)
  recipient <- contract_validator;

  call_add_app = {
    _tag: "AddApp";
    _recipient: recipient;
    _amount: Uint128 0;
    app: app;
    owner: _sender
  };

  msgs = one_msg call_add_app;

  send msgs
end

procedure SendADToCheck(ad: Banner, owner: ByStr20, amount: Uint128)
  recipient <- contract_validator;

  call_add_ad = {
    _tag: "AddAD";
    _recipient: recipient;
    _amount: Uint128 0;
    banner: ad;
    owner: owner
  };

  msgs = one_msg call_add_ad;

  send msgs
end

(* Admin transitions *)
transition ConfigureCategory(value: Uint32)
  IsContractOwner;

  max_category := value;

  e = {
    _eventname : "ConfigureCategoryEvent";
    max_category: value
  };
  event e
end

transition ConfigureBlockDecimal(value: Uint128)
  IsContractOwner;

  block_decimal := value;

  e = {
    _eventname : "ConfiguredBlockDecimalEvent";
    new_block_decimal: value
  };
  event e
end

transition ConfigureAppPrice(new_price: Uint128)
  IsContractOwner;

  old_price <- price_for_app;

  price_for_app := new_price;

  e = {
    _eventname : "ConfiguredAppPriceEvent";
    old_price: old_price;
    new_price: new_price
  };
  event e
end
(* Admin transitions *)

(* User transitions *)
transition AddApp(title: String, des_url: String, url: String, ipfs_image: List String, ipfs_icon: String, category: Uint32)
  CheckCategory category;

  price <- price_for_app;

  TransferFrom price _sender;

  new_app = App title des_url url ipfs_image ipfs_icon category;

  SendAppToCheck new_app
end

transition AddAD(amountZLP: Uint128, url: String, banner_url: String)
  CheckPriceForAD amountZLP;
  TransferFrom amountZLP _sender;
  
  decimal <- block_decimal;

  blocks = zlp_to_blocks amountZLP decimal;
  new_ad = Banner blocks url banner_url;

  SendADToCheck new_ad _sender amountZLP
end
(* User transitions *)

(* Callbacks *)
transition ConfigureCoefficient(amount: Uint256, op: Operation)
  IsValidator;
  value <- coefficient;

  new_coefficient = get_coefficient value amount op;
  coefficient := new_coefficient
end

transition TransferFromSuccessCallBack(initiator: ByStr20, sender: ByStr20, recipient: ByStr20, amount: Uint128)
end

transition TransferSuccessCallBack(sender: ByStr20, recipient: ByStr20, amount: Uint128)
end
(* Callbacks *)
